VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "ThisWorkbook"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Option Explicit

Private Const Caption As String = "Git管理"
Private newEventsItems() As IClassBase

Private Type ItemProperties
    FullName As String
    Caption As String
    OnAction As String
    FaceId As Long
    BeginGroup As Boolean
End Type

Private Function MenuItemSetting() As ItemProperties()
    
    Dim prop() As ItemProperties
    
    Dim i As Long
    
    ' ---- メニュー開始 --------------------------------------------------

    i = 0: ReDim prop(i)
    prop(i).Caption = "Gitファイルを出力"
    prop(i).OnAction = "ModuleGitFiles.GenerateGitFiles"
    prop(i).FaceId = 1552
    prop(i).BeginGroup = False
    
    i = i + 1: ReDim Preserve prop(i)
    prop(i).Caption = "コードモジュールを出力"
    prop(i).OnAction = "ModuleMenu.Decombine"
    prop(i).FaceId = 461
    prop(i).BeginGroup = True
    
    
    ' ---- メニュー終了 --------------------------------------------------
    
    Dim FullName As String: FullName = ThisWorkbook.FullName
    For i = 0 To UBound(prop)
        prop(i).FullName = FullName
    Next
    
    MenuItemSetting = prop

End Function

Private Sub Workbook_BeforeClose(Cancel As Boolean)
    
    ' 配列がリセットされてしまっている場合への対応
    If IsArrayInitialized(newEventsItems) Then
    
        Dim i As Long
        For i = 0 To UBound(newEventsItems)
            Set newEventsItems(i) = Nothing
        Next
        
        Erase newEventsItems
        
    End If
    
    ' VBEのメニューバーを取得（メニューバーのIndexは、固定値で「1」）
    Dim VBEMenuBar As CommandBar
    Set VBEMenuBar = Application.VBE.CommandBars(1)
    
    Dim itm As CommandBarControl
    For Each itm In VBEMenuBar.Controls(Caption).Controls
        itm.Delete
    Next
    VBEMenuBar.Controls(Caption).Delete
    
End Sub

Private Sub Workbook_Open()
    
    ' VBEのメニューバーを取得（メニューバーのIndexは、固定値で「1」）
    Dim VBEMenuBar As CommandBar
    Set VBEMenuBar = Application.VBE.CommandBars(1)
    
    ' 新しいメニューを作成
    Dim newMenu As CommandBarControl
    Set newMenu = VBEMenuBar.Controls.Add(Type:=msoControlPopup)
    
    ' メニューのキャプションを設定
    newMenu.Caption = Caption
    
    Dim prop() As ItemProperties: prop = MenuItemSetting
    ReDim newEventsItems(UBound(prop))
    Dim i As Long, newMenuItem As CommandBarControl
    For i = 0 To UBound(prop)
        ' メニューにアイテムを追加
        Set newMenuItem = newMenu.Controls.Add(Type:=msoControlButton)
        With prop(i)
            newMenuItem.Caption = .Caption
            newMenuItem.OnAction = "'" & .FullName & "'!" & .OnAction
            newMenuItem.FaceId = .FaceId
            newMenuItem.BeginGroup = .BeginGroup
        End With
        ' イベントクラスの登録
        Set newEventsItems(i) = New EventsItem: Call newEventsItems(i).Initialize(newMenuItem)
    Next
    
End Sub

Private Sub Workbook_Open2()
    
    ' VBEのメニューバーを取得（メニューバーのIndexは、固定値で「1」）
    Dim VBEMenuBar As CommandBar
    Set VBEMenuBar = Application.VBE.CommandBars.Add("Git管理")
    
    ' 新しいメニューを作成
    Dim newMenu As CommandBarControl
'    Set newMenu = VBEMenuBar.Controls.Add(Type:=msoControlButton)
    
    ' メニューのキャプションを設定
'    newMenu.Caption = Caption
    
    Dim prop() As ItemProperties: prop = MenuItemSetting
    ReDim newEventsItems(UBound(prop))
    Dim i As Long, newMenuItem As CommandBarControl
    For i = 0 To UBound(prop)
        ' メニューにアイテムを追加
        Set newMenuItem = VBEMenuBar.Controls.Add(Type:=msoControlButton)
        With prop(i)
            newMenuItem.Caption = .Caption
            newMenuItem.OnAction = "'" & .FullName & "'!" & .OnAction
            newMenuItem.FaceId = .FaceId
            newMenuItem.BeginGroup = .BeginGroup
        End With
        ' イベントクラスの登録
        Set newEventsItems(i) = New EventsItem: Call newEventsItems(i).Initialize(newMenuItem)
    Next
    VBEMenuBar.Visible = True
End Sub


Function IsArrayInitialized(arr() As IClassBase) As Boolean
    On Error GoTo Catch
    IsArrayInitialized = IsNumeric(UBound(arr))
    Exit Function
Catch:
    IsArrayInitialized = False
End Function


Private Sub test1()
    Workbook_BeforeClose False
End Sub

Private Sub test2()
    Dim cmdbar As CommandBar
    For Each cmdbar In Application.VBE.CommandBars
        Debug.Print Format(cmdbar.Index, "00") & " - " & cmdbar.Name
    Next
End Sub

