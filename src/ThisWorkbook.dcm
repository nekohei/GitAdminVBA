VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "ThisWorkbook"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
Option Explicit

Rem イベント配列
Private WithEvents mGitEventsItems As GitEventsItems
Attribute mGitEventsItems.VB_VarHelpID = -1
Private Const mCaption As String = "Git管理(&G)"

Rem イベントから伝播されたメニュー項目を判別して処理分岐
Private Sub mGitEventsItems_Click(ByVal ctl As Object, handled As Boolean, cancelDefault As Boolean)
    Rem Indexは登録順に1から自動的に振られる
    Select Case ctl.Index
        Case 1: Call CreateNewRepository
        Case 2: Call GitStage
        Case 3: Call GitCommit
        Case 4: Call GitPush
        Case 5: Call RegisterToken
        Case 6: Call DeleteToken
    End Select
End Sub

Private Sub Workbook_BeforeClose(ByRef Cancel As Boolean)
    On Error GoTo Catch
    Rem ワークシート編集をした場合に備えて常にアドインとして再設定
    Me.IsAddin = True
    Rem VBEのメニューバーを取得（メニューバーのIndexは、固定値で "1"）
    Dim VBEMenuBar As CommandBar: Set VBEMenuBar = Application.VBE.CommandBars(1)
    Rem メニュー項目を削除
    Dim itm As CommandBarControl
    For Each itm In VBEMenuBar.Controls(mCaption).Controls
        itm.Delete
    Next
    Rem メニュー自体を削除
    VBEMenuBar.Controls(mCaption).Delete
    Rem イベント配列を解放
    Set mGitEventsItems = Nothing
    Exit Sub
Catch:
    PrintErr Err, "Workbook_BeforeClose"
End Sub

Private Sub Workbook_Open()
    Rem VBEのメニューバーを取得（メニューバーのIndexは、固定値で "1"）
    Dim VBEMenuBar As CommandBar: Set VBEMenuBar = Application.VBE.CommandBars(1)
    Rem 新しいメニューを作成
    Dim newMenu As CommandBarControl: Set newMenu = VBEMenuBar.Controls.Add(Type:=msoControlPopup)
    newMenu.Caption = mCaption
    Rem イベント配列を生成
    Set mGitEventsItems = New GitEventsItems
    Rem Sheet1の入力内容を基にメニューを組み立て
    With Sheet1
        Dim rw As Long: rw = 2
        Do Until IsEmpty(.Cells(rw, 1).Value)
            Rem メニュー項目を追加
            Dim newMenuItem As CommandBarControl: Set newMenuItem = newMenu.Controls.Add(Type:=msoControlButton)
            newMenuItem.Caption = .Range("Caption").Cells(rw).Value
            newMenuItem.FaceId = .Range("FaceId").Cells(rw).Value
            newMenuItem.BeginGroup = CBool(.Range("BeginGroup").Cells(rw).Value)
            Rem イベント配列にメニュー項目を登録
            Call mGitEventsItems.AddItem(newMenuItem)
            rw = rw + 1
        Loop
    End With
End Sub

